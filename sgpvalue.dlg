/*
sgpvalue dialog version 0.98a, 15.03.2020, Sven-Kristjan Bormann
for sgpvalue version 0.98a

To install in the User Statistics menu via Stata commands:
  
  . window menu append item "stUserStatistics" "SGPV Value Calculations (&sgpvalue)" "db sgpvalue"

  . window menu refresh

To permanently install, place the commands in your -profile.do- file.
*/
VERSION 12.0
INCLUDE _std_medium
POSITION . . 400 250
OK ok1,      label("OK")
CANCEL can1, label("Cancel")
SUBMIT sub1, label("Submit") 
COPY cop1
HELP hlp1,   view("help sgpvalue")  
RESET res1


DIALOG main, label("Sgpvalue 0.98a - SGPV Value Calculations") tabtitle("Main")
BEGIN
	GROUPBOX gb_est	 	_lft  _top  220    110, label("Estimated Interval") 
	TEXT tx_estlo 		+10   _ss   80      ., label("Lower Bound") 
	EXP ex_estlo	 	@     _ss   200     ., option(estlo) label("Expression") /// 
										   	tooltip("Set lower bound of the estimated interval") error(Lower Bound estimated interval)
										   
	TEXT tx_esthi	 	20   +20    80      ., label("Upper Bound")
	EXP ex_esthi	 	@ 	 _ss    200     ., option(esthi) label("Expression") ///
										   	tooltip("Set upper bound of the estimated interval") error(Upper Bound estimated interval)
										   
	GROUPBOX gb_null   _lft  +40    220    110, label("Null-Hypothesis Interval")
	TEXT tx_nulllo 		+10  _ss    80       ., label("Lower Bound") 
	EXP ex_nulllo		@    _ss    200      ., label("Expression")  option(nulllo) ///
										   	tooltip("Set lower bound of the null-hypothesis interval") error(Lower Bound null-hypothesis interval)
										   
	TEXT tx_nullhi 		20   +20     80      ., label("Upper Bound")
	EXP ex_nullhi	 	@	 _ss    200      ., label("Expression") option(nullhi) ///
										   	tooltip("Set upper bound of the null-hypothesis interval") error(Upper Bound null-hypothesis interval)
										   
	CHECKBOX ck_nowarn  260  15    80        ., label("No Warnings") option(nowarnings) ///
										   	tooltip("Do not display no warning messages about problematic interval(s)")
										   
	CHECKBOX ck_nodelta @   _ss     @        ., label("No Delta-Gap") option(nodeltagap) tooltip("Do not calculate and display delta-gaps.")
	CHECKBOX ck_nomata  @   _ss     @        ., label("No Mata") option(nomata) ///
										   	 tooltip("Do not use the Mata routines for large input matrices and instead use variables for the calculations.")
	CHECKBOX ck_replace @   _ss     @        ., label("Replace") option(replace) ///
										   	tooltip("Replace existings variables if 'No Mata' option is set or variables are used as inputs for the estimated interval bounderies.")
	CHECKBOX ck_noshow @   _ss      @        ., label("No Show") option(noshow) ///
										   	tooltip("Do not show the calculated results which can be useful for large amount of calculated SGPVs and delta-gaps.")
										   
	EDIT ed_inf        @   _xxls    @ 	     ., option(infcorrection) default(1e-5) numonly ///
										   	tooltip("A small scalar to denote a positive but infinitesimally small SGPV. Default is 1e-5. SGPVs that are infinitesimally close to 1 are assigned 1-infcorrection. This option can only be invoked when one of the intervals has infinite length.")
	TEXT tx_inf       @     _ss    100      ., label("Infinity correction")
	
END

PROGRAM command 
BEGIN
	put "sgpvalue "
	
	beginoptions
		require main.ex_estlo
		optionarg main.ex_estlo
		
		require main.ex_esthi
		optionarg main.ex_esthi
		
		require main.ex_nulllo
		optionarg main.ex_nulllo
		
		require main.ex_nullhi
		optionarg main.ex_nullhi
		
		option main.ck_nowarn
		option main.ck_nomata
		option main.ck_nodelta
		option main.ck_replace
		option main.ck_noshow
		optionarg main.ed_inf
	endoptions	
	
END



