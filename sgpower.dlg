/*
sgpower dialog version 0.92, 13.03.2020, Sven-Kristjan Bormann
for sgpower version 0.92

To install in User Statistics menu via Stata commands:
  
  . window menu append item "stUserStatistics" "SGPV Power Calculations (&sgpower)" "db sgpower"

  . window menu refresh

To permanently install, place the commands in your -profile.do- file.
*/

VERSION 12.0
INCLUDE _std_small
INCLUDE header
HELP hlp1,   view("help sgpower")  
RESET res1
*SUBMIT sub1, label("Submit")

LIST inttype_list
BEGIN
	confidence
	likelihood
END

DIALOG main, label("Sgpower 0.92 - SGPV Power Calculations")
BEGIN
	TEXT tx_true 		_lft    30   100    ., label("True value")
	EDIT ed_true 		 @    _ss    100    ., numonly option(true) error("A true value is required.")
	
	GROUPBOX gb_null	 120  _top   220   80, label("Null-Hypothesis Interval")
	TEXT tx_nulllo 		+10   _ss 	 80     ., label("Lower Bound") 
	EDIT ed_nulllo	 	@     _ss    80     ., option(nulllo) default(0) numonly ///
											error("A lower bound for the null interval is required.")
	
	TEXT tx_nullhi	 	+100   -20    80    ., label("Upper Bound")
	EDIT ed_nullhi	 	@     _ss     80    ., option(nullhi) default(0) numonly ///
											error("An upper bound for the null interval is required.")
	
	TEXT tx_se			-100  _xls    80    ., label("Standard Error")
	EDIT ed_se			@     _ss     @     ., option(stderr) default(1) numonly
	
	CHECKBOX ck_bonus   @    _xls     100   ., label("Bonus statistics") option(bonus) ///
											tooltip("Display additional statistics")
	
	TEXT tx_inttpye   _lft   -50      @     ., label("Interval type:")
	COMBOBOX cb_inttype @    _ss      @     ., dropdownlist content(inttype_list) option(inttype)
	
	TEXT tx_intlevel    @    _ls      @     ., label("Interval level:")
	EDIT ed_intlevel    @    _ss      @     ., option(intlevel) error("An interval level is required.")
	
	
END

PROGRAM command
BEGIN
	put "sgpower "
	beginoptions
		require main.ed_true
		 optionarg main.ed_true
		 
		 require main.ed_nulllo
		 optionarg main.ed_nulllo
		 
		 require main.ed_nullhi
		 optionarg main.ed_nullhi
		 
		 require main.cb_inttype
		 optionarg main.cb_inttype
		 
		 require main.ed_intlevel
		 optionarg main.ed_intlevel
		 
		 optionarg main.ed_se
		 option main.ck_bonus
	endoptions

END
